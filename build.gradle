buildscript {
    ext.versions = [
        compileSdkVersion: 30,
        minSdkVersion    : 21,
        targetSdkVersion : 30,
        kotlin           : "1.4.10",
        coil             : "1.0.0",
        constraintLayout : "2.0.4",
        gson             : "2.8.6",
        livedata         : "2.2.0",
        photoview        : "2.3.0",
        viewmodel        : "2.2.0",
        navigation       : "2.3.1",
        timber           : "4.7.1",
        ktlint           : '0.36.0',
        layercache       : '1.2.1',
        systemuihelper   : '1.0.0'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.0-alpha16"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.6.1"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
        apply plugin: 'androidx.navigation.safeargs'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-android-extensions'

    android {
        compileSdkVersion versions.compileSdkVersion

        defaultConfig {
            applicationId "dev.gowtham.nasapictures"
            minSdkVersion versions.minSdkVersion
            targetSdkVersion versions.targetSdkVersion
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        signingConfigs {
            release {
                if (project.hasProperty('RELEASE_STORE_FILE')) {
                    storeFile file(RELEASE_STORE_FILE)
                    storePassword RELEASE_STORE_PASSWORD
                    keyAlias RELEASE_KEY_ALIAS
                    keyPassword RELEASE_KEY_PASSWORD
                }
            }
        }

        buildTypes {
            release {
                debuggable false
                jniDebuggable false
                minifyEnabled true
                shrinkResources true
                signingConfig signingConfigs.release
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}

spotless {
    lineEndings 'UNIX'

    kotlin {
        target "**/*.kt"
        ktlint(versions.ktlint).userData([
            'indent_style'            : 'space',
            'indent_size'             : '4',
            'continuation_indent_size': '8'
        ])
        trimTrailingWhitespace()
        endWithNewline()
    }
}
